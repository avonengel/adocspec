plugins {
    id 'java'
    id 'org.asciidoctor.convert' version '1.5.9.2'
    id "org.itsallcode.openfasttrace" version "0.6.0"
}

repositories {
    jcenter()
}

version = '0.1.0-SNAPSHOT'

asciidoctorj {
    version = '1.6.2'
}
configurations.asciidoctor.extendsFrom configurations.implementation
dependencies {
    asciidoctor sourceSets.main.output
    asciidoctor 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'org.asciidoctor:asciidoctorj:1.6.2'
    implementation 'org.itsallcode:openfasttrace:2.2.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    // Use JUnit test framework
    testImplementation enforcedPlatform('org.junit:junit-bom:5.4.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.apache.commons:commons-lang3:3.8.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'org.assertj:assertj-core:3.11.1'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

test {
    useJUnitPlatform()
}

asciidoctor {
    backends = ['html5', 'spec']
    outputDir = docsDir
    separateOutputDirs = false
    attributes << [
            'build-gradle'      : file('build.gradle'),
            'sourcedir'         : project.sourceSets.main.java.srcDirs[0],
            'source-highlighter': 'coderay',
            'toc'               : 'left',
            'icons'             : 'font',
            'project-version'   : version,
            'specfile'          : new File(buildDir, "spec.reqm.xml"),
            'javadoc-dir'       : 'javadoc',
    ]
}

//noinspection UnnecessaryQualifiedReference
task oftAdocspec(type: org.asciidoctor.gradle.AsciidoctorTask) {
    inputs.files(classes)
    sourceDir = file('src/test/resources/oft')
    outputDir = new File(project.buildDir, 'oft')
    separateOutputDirs = false
    backends = ['html5', 'spec']
}

requirementTracing {
    inputDirectories = files(asciidoctor.outputDir, 'src/main', 'src/test/java')
    reportFormat = 'html'
}
collectRequirements.dependsOn(asciidoctor)

configurations {
    asciidoclet
}

dependencies {
    asciidoclet 'org.asciidoctor:asciidoclet:1.5.6'
}

javadoc {
    // NOTE: does not work with JDK 9+: https://github.com/asciidoctor/asciidoclet/issues/71
    def overviewFile = 'src/main/java/overview.adoc'
    inputs.file(overviewFile)
    options.docletpath = configurations.asciidoclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.overview = overviewFile
    options.addStringOption "-base-dir", "${projectDir}"
    options.addStringOption "-attribute",
            "version=${project.version}," +
                    "gh-url=https://github.com/avonengel/adocspec," +
                    "design=../html5/design.html," +
                    "src-with-package=src/main/java/com/github/avonengel/adocspec"
}

task documentation(dependsOn: [javadoc, asciidoctor]) {
    group 'documentation'
    description 'Creates all documentation'
}

task docZip(type: Zip) {
    archiveBaseName = 'documentation'
    from docsDir
}